plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

group = rootProject.group + ".test"

graalvmNative {
    binaries {
        main {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(21)
            }
            imageName = "connection-utility"
            mainClass = "com.jcommsarray.test.ConnectionUtility"

            sharedLibrary = false
            useFatJar = true
        }
    }
    agent {
        enabled = project.hasProperty("graalvmAgent")
        defaultMode = "direct"

        modes {
            direct {
                options.add("config-output-dir=src/main/resources/META-INF/native-image")
            }
        }

        builtinCallerFilter = true
        builtinHeuristicFilter = true
        enableExperimentalPredefinedClasses = false
        enableExperimentalUnsafeAllocationTracing = false
        trackReflectionMetadata = true
    }
}

application {
    mainClass = "com.jcommsarray.test.ConnectionUtility"
}

dependencies {
    implementation project(":client")

    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
}